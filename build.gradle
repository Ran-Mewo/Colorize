plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id "io.github.rancraftplayz.remapper" version "1.0.2"
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    mavenCentral()
    maven { url = 'https://repo.spongepowered.org/maven' }
    maven { url = "https://maven.elmakers.com/repository/" }
    maven { url = "https://jitpack.io" }
    mavenLocal()
}

configurations {
    shade // The things to put in the shadowJar
    implementation.extendsFrom remapLib // Make the remapLib show up as a dependency
    implementation.extendsFrom shade // Make the shaded stuff show up as a dependency
    accessWidenerLib.extendsFrom implementation // Make so the access wideners will be applied to all implemented dependencies
}

dependencies {
    // Ignite & Spigot
    remapLib "org.spigotmc:spigot:${project.spigot_version}:remapped-mojang"
    implementation "space.vectrix.ignite:ignite-api:${project.ignite_version}"
    implementation "org.spongepowered:mixin:${project.mixin_version}"

    shade("com.github.Ran-helo:Ranitils:f10fa89021") {
        exclude group: 'com.google.inject', module: 'guice'
    }
}

spigot {
    version = project.minecraft_version
}

processResources {
    inputs.property "version", project.version

    filesMatching("ignite.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.getByName("shade")]
    classifier ''
    mergeServiceFiles()
}
jar.finalizedBy(shadowJar)
shadowJar.finalizedBy(remapJar)